apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

// REQUIRED:
// apply from: "$rootDir/project-config/repositories.gradle"
repositories artifactory

[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

compileJava {
    def javaVersion = System.properties.javaVersion
    if (javaVersion != null) {
        def javaVersionValue = Float.valueOf(javaVersion)
        sourceCompatibility = javaVersionValue
        targetCompatibility = javaVersionValue
    } else {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    options.encoding = "UTF-8"
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = false
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    if (System.properties.uploadSources != null) {
        archives sourcesJar
    }
    // archives javadocJar
}

dependencies {
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.9.5"
}
